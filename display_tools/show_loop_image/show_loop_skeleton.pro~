;+
; NAME:
;      show_loop_skeleton
; PURPOSE
;      given a field line and a set of radii, draw a sekeloon
;      consisting of rings around each point in the field line
; USAGE:
;      show_loop_skeleton, fl, rad
; KEYWORDS:
;      view = view: will transform according to transformation in
;             structure view.
;      threed: show field line in 3d
;      skip=skip: if set then only every skip ribs are rendered. [1]
;-

PRO show_loop_skeleton, fl, rad, threed=threed, view=view, $
  skip=skip, CIRC=CIRC, C_STRUCT=C_STRUCT,$
  LINESTYLE=LINESTYLE,NO_PLOT=NO_PLOT,C_COLOR=C_COLOR

IF( NOT keyword_set(skip) ) THEN skip = 1
IF( NOT keyword_set(nth) ) THEN nth = 41
IF( NOT keyword_set(LINESTYLE)) THEN LINESTYLE=0
;axis, zaxis=-1, xaxis=-1, yaxis=-1
nfl =  n_elements( fl(0, *) )
;Create an array that will define a 2d circle in a 3d plane
; with nth points on its rim
circ = fltarr( 3,  nth )
th =  2*!pi*findgen(nth)/float(nth-1)

r =  rad
IF( n_elements(rad) EQ 1 ) THEN r =  replicate( rad, nfl )

FOR i=0, nfl-1, skip DO BEGIN
    IF( i EQ 0 ) THEN BEGIN
        dx = reform( fl(*, 1) - fl(*, 0),  3 )
    ENDIF ELSE IF( i EQ nfl-1 ) THEN BEGIN 
        dx = reform( fl(*, nfl-1) - fl(*, nfl-2),  3 )
    ENDIF ELSE BEGIN
        dx =  reform( fl(*, i+1) - fl(*, i-1),  3 )
    ENDELSE

  ; the elements of the skeleton: circles
    triad, dx, e1, e2
    circ(0, *) = fl(0, i) + r(i)*( cos(th)*e1(0) + sin(th)*e2(0) )
    circ(1, *) = fl(1, i) + r(i)*( cos(th)*e1(1) + sin(th)*e2(1) )
    circ(2, *) = fl(2, i) + r(i)*( cos(th)*e1(2) + sin(th)*e2(2) )
    if not keyword_set(NO_PLOT) then begin
        IF( n_elements( view ) GT 0 ) THEN fl_view_xform, circ, view
        IF( keyword_set( threed ) ) THEN $
          plots, circ(0, *), circ(1, *), circ(2, *), $
                 /t3d ,LINESTYLE=LINESTYLE,COLOR=C_COLOR $
          ELSE BEGIN
            oplot, circ(0, *), circ(1, *),LINESTYLE=LINESTYLE,$
                   COLOR=C_COLOR
        ENDELSE
    endif
    if size(c_struct, /TYPE) ne 8 then  $
      c_struct={circ:circ} else $
      c_struct=concat_struct(c_struct, {circ:circ})
ENDFOR

return
end
